buildscript {
    ext.kotlin_version = '1.3.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
plugins {
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.ben-manes.versions" version "0.22.0"
}

def artifactVersion = '1.2.3'
def artifactGroup = 'com.chimerapps.gradle'
def artifactName = 'gradle-appcenter-android-plugin'
group "$artifactGroup"
version "$artifactVersion"

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlin-kapt'
apply plugin: 'java-gradle-plugin'
apply plugin: 'maven-publish'
apply plugin: 'idea'

sourceCompatibility = 1.7
targetCompatibility = 1.7

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from 'docs/README.txt'
}

repositories {
    google()
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    compile 'com.squareup.retrofit2:retrofit:2.6.1'
    compile 'com.squareup.okhttp3:okhttp:4.1.1'
    compile 'com.squareup.okhttp3:logging-interceptor:4.1.1'
    compile 'com.squareup.moshi:moshi:1.8.0'
    compile 'com.squareup.retrofit2:converter-moshi:2.6.1'

    compile 'com.chimerapps:moshi-generator-runtime:0.10.0'
    kapt 'com.chimerapps:moshi-generator:0.10.0'

    compileOnly "com.android.tools.build:gradle:3.0.1"

    testCompile 'junit:junit:4.12'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    pkg {
        repo = 'maven'
        name = "$artifactName"
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Chimerapps/gradle-appcenter-android-plugin'

        version {
            name = "$artifactVersion"
            desc = "Gradle plugin for uploading builds to appcenter"
            released = new Date()
        }
    }
    publications = ['maven']
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Nicola Verbeeck"
            email "nicola@chimerapps.com"
            organization "Chimerapps"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$artifactGroup"
            artifactId "$artifactName"
            version "$artifactVersion"

            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Gradle plugin that allows android builds to be uploaded to appcenter')
                root.appendNode('name', "$artifactName")
                root.appendNode('url', 'https://github.com/Chimerapps/gradle-appcenter-android-plugin')
                root.children().last() + pomConfig
            }
        }
    }
}
